;******************************************************************************
;Description:                                                                 * 
; 3-563.asm                                                                   *
;        DSP56300                                                             *
;        67 tap FIR filter 					              *	
;                                                                             *  
;******************************************************************************
;  REVISION HISTORY:                                                          *
;   Date           Change                                                     *
;   June 30,1988    Initial placement                                         *
;   Aug  24,1998    Implementation on DSP56300 	                              *
;******************************************************************************
;
;	This FIR filter reads the input sample
;	from the memory location Y:input
;	and writes the filtered output sample
;	to the memory location Y:output
;
;	The samples are stored in the X memory
;	The coefficients are stored in the Y memory
;
;
;          X MEMORY                               Y MEMORY
;
;         |        |                             |        |
;    R0   |--------|                             |--------|
;  +----->|  X(n)  |                      +----->|  c(0)  |
;  |  t   |--------|                      |t,t+T |--------|
;  |      | X(n-1) |                      |      |  c(1)  |
;  |      |--------|                      |      |--------|
;  |      |        |                      |      |        |
;  |      |        |                      |      |        |
;  |      |        |                      |      |        |
;  |      |        |                      |      |        |
;  |      |--------|                      |      |--------|
;  +----->|X(n-k+1)|  X(n+1)              +<-----| c(k-1) |
;   t+T   |--------|                             |--------|
;         |        |                             |        |
;
;
;                              C(0)                      
;                              ___          ___
;    x(n)                     /   \        /   \         y(n)
;    -------------+----------|  X  |----->|  +  |--------->
;                 |           \___/        \___/
;                 |                          ^             k-1
;                 |                          |             ____
;              +-----+                       |             \   '
;              |  T  |         C(1)          |      y(n)=  /___,c(p)x(n-p)
;              +-----+         ___           |             p=0
;                 |           /   \          |
;                 +----------|  X  |-------->+  
;                 |           \___/          |
;              +-----+                       |
;              |  T  |         C(2)          |
;              +-----+         ___           |
;                 |           /   \          |
;                 +----------|  X  |-------->+   
;                 |           \___/          |
;                 |                          |
;                 |                          |
;                 |                          |
;                 |                          |
;                 |                          |
;                 |                          |
;              +-----+                       |
;              |  T  |         C(K-1)        |
;              +-----+         ___           |
;                 |           /   \          |
;                 +----------|  X  |-------->+     
;                             \___/
;
;
;                            F I R
;
;**************************************************************************

 
n	equ	67			;initialization
start	equ	$000100
wddr	equ	$000080
cddr	equ	$000080
input	equ	$ffffe0
output	equ	$ffffe1
      
       org y:$000040
          
coeff        
  DC  0.0007,0.0008,0.0005,0.0000,-0.0007,-0.0014,-0.0017
  DC -0.0013,0.0000,0.0019,0.0036,0.0043,0.0031,0.0000
  DC -0.0042,-0.0080,-0.0092,-0.0066,0.0000,0.0087,0.0161
  DC  0.0184,0.0131,0.0000,-0.0175,-0.0330,-0.0389,-0.0289
  DC  0.0000,0.0452,0.0989,0.1499,0.1865,0.1997,0.1865
  Dc  0.1499,0.0989,0.0452,0.0000,-0.0289,-0.0389,-0.0330
  DC -0.0175,0.0000,0.0131,0.0184,0.0161,0.0087,0.0000
  DC -0.0066,-0.0092,-0.0080,-0.0042,0.0000,0.0031,0.0043
  Dc  0.0036,0.0019,0.0000,-0.0013,-0.0017,-0.0014,-0.0007
  Dc  0.0000,0.0005,0.0008,0.0007

	org	p:start
	move 	#wddr,r0		;r0 -> samples
	move	#cddr,r4		;r4 -> coefficients
	move 	#n-1,m0			;set modulo arithmetic
	move	m0,m4			;for the 2 circular buffers

	opt	cc
       do #28,_fff	
        movep 	y:input,x:(r0)		;input sample in memory
	clr	a	x:(r0)+,x0	y:(r4)+,y0
 
	rep	#n-1
	mac	x0,y0,a	x:(r0)+,x0 	y:(r4)+,y0
	macr	x0,y0,a	(r0)-
	movep	a,y:output		;output filtered sample
_fff 
;***************************************************************
	end


